/*
 * Copyright 2013 (c) MuleSoft, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND,
 * either express or implied. See the License for the specific
 * language governing permissions and limitations under the License.
 */
apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives sourceJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description project.description
                    url 'http://raml.org/'
                    scm {
                        delegate.url 'scm:git@github.com:mulesoft/raml-jaxrs-codegen.git'
                        connection 'scm:git@github.com:mulesoft/raml-jaxrs-codegen.git'
                        developerConnection 'scm:git@github.com:mulesoft/raml-jaxrs-codegen.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'jdpgrailsdev'
                            name 'Jonathan Pearlin'
                        }
                    }
                }

            }
        }
    }

    repositories {
        maven {
            name 'snapshots'
            url 'https://repository.mulesoft.org/snapshots'
            credentials {
                // Use -PrepoUsername and -PrepoPassword command line properties
                username = repoUsername
                password = repoPassword
            }
        }
        maven {
            name 'releases'
            url 'https://repository.mulesoft.org/releases'
            credentials {
                // Use -PrepoUsername and -PrepoPassword command line properties
                username = repoUsername
                password = repoPassword
            }
        }
        maven {
            name 'localrepo'
            url localRepoUrl				// Use -PlocalRepoUrl command line property.
            credentials {
                // Use -PrepoUsername and -PrepoPassword command line properties
                username = repoUsername
                password = repoPassword
            }
        }
    }
}